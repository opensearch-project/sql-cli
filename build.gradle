plugins {
    id 'java'
    id 'application'
    id 'base'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.diffplug.spotless' version '7.1.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'Gateway'
}

repositories {
    mavenCentral()
    maven {
        url "https://aws.oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url "https://jitpack.io"
    }
}

// Helper function for version detection
ext.getEffectiveVersion = { ->
    // Look for task names with version format (e.g., 3_1_0_0 or 3_x)
    def taskName = gradle.startParameter.taskNames.find { it.count("_") >= 1 && it.matches("[0-9].*") }
    if (taskName != null) {
        // Check if it's a local version task (ends with _local)
        if (taskName.endsWith("_local")) {
            return taskName.substring(0, taskName.lastIndexOf("_local")).replace('_', '.')
        }
        return taskName.replace('_', '.')
    }
    // Use default version when no version is specified
    return project.ext.defaultVersion
}

// Helper function to check if a task is for local JAR
ext.isLocalJarTask = { String taskName ->
    return taskName.endsWith("_local")
}

// Helper function to get local JAR directory
ext.getLocalJarDir = { ->
    return project.hasProperty('localJarDir') ? project.getProperty('localJarDir') : null
}

ext.updateIsV3orAbove = { String version ->
    project.ext.isV3orAbove = version?.startsWith("3") ?: false
    return project.ext.isV3orAbove
}

ext.getOpenSearchClientVersion = { String version ->
    def parts = version.split("\\.")
    // 2.19.1 -> 2.19.0 format for OpenSearch client dependencies
    if (parts.length >= 3) {
        return "${parts[0]}.${parts[1]}.0"
    }
    return version
}

// Helper function to determine which HTTP client to use based on version
ext.getHttpClientInfo = { ->
    def result = [:]
    
    if (project.ext.isV3orAbove) {
        result.clientName = "HTTP5"
        result.sourceSet = sourceSets.http5
        result.dependencies = configurations.http5Dependency
    } else {
        result.clientName = "HTTP4"
        result.sourceSet = sourceSets.http4
        result.dependencies = configurations.http4Dependency
    }
    
    return result
}


ext {
    // Default version to use when no specific version is specified
    defaultVersion = "3.1.0.0"
    
    submodules = ['common', 'core', 'opensearch', 'ppl', 'sql', 'protocol', 'datasources']

    // TODO: will need to see why using datasources on Maven fails
    // but using local jar file works
    mavenSubmodules = ['common', 'core', 'opensearch', 'ppl', 'sql', 'protocol']

    // Global variable to determine if version is 3 or above
    // Initialize with default value, will be updated when needed
    isV3orAbove = false
    
    // Update isV3orAbove based on current version
    updateIsV3orAbove(getEffectiveVersion())

    // Common dependencies shared across all versions
    sharedDeps = [
        // OpenSearch
        'org.apache.calcite:calcite-core:1.40.0',
        'com.facebook.presto:presto-matching:0.293',
        'org.antlr:antlr4-runtime:4.7.1',
        'org.apache.commons:commons-lang3:3.17.0',
        'org.reactivestreams:reactive-streams:1.0.4',
        'com.google.guava:guava:32.4.8-jre',
        // AWS SDK v2 
        'software.amazon.awssdk:sdk-core:2.31.63',
        'software.amazon.awssdk:auth:2.31.63',
        'software.amazon.awssdk:regions:2.31.63',
        'software.amazon.awssdk:apache-client:2.31.63',
        'software.amazon.awssdk:sts:2.31.63',
        'software.amazon.awssdk:aws-core:2.31.63',
        // Logging 
        'org.apache.logging.log4j:log4j-core:2.25.0',
        'org.apache.logging.log4j:log4j-api:2.25.0',
        // Logback
        'ch.qos.logback:logback-classic:1.5.18',
        // Apache Commons Configuration for YAML file parsing
        'org.apache.commons:commons-configuration2:2.12.0',
        'commons-beanutils:commons-beanutils:1.11.0',
        'org.yaml:snakeyaml:2.2',
        // JSON  
        'org.json:json:20250517',
        'com.google.code.gson:gson:2.13.1',
        // Guice, dependency injection
        'com.google.inject:guice:7.0.0',
        // Py4J
        'net.sf.py4j:py4j:0.10.9.9'
    ]
    
    // HTTP5 dependencies for v3 and above
    http5Deps = [
        'org.apache.httpcomponents.core5:httpcore5:5.2',
        'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    ]
    
    // HTTP4 dependencies for below v3
    http4Deps = [
        'org.apache.httpcomponents:httpcore:4.4.16',
        'org.apache.httpcomponents:httpclient:4.5.14',
        'io.github.acm19:aws-request-signing-apache-interceptor:3.0.0'
    ]
        
    // OpenSearch client dependencies
    opensearchClientDeps = [
        'opensearch-rest-high-level-client',
        'opensearch-rest-client',
        'opensearch-java'
    ]
    
    metaInfExclusions = [
        'META-INF/*.SF',
        'META-INF/*.DSA',
        'META-INF/*.RSA',
        'META-INF/*.EC',
        'META-INF/MANIFEST.MF'
    ]
    
    jvmArgs = [
        '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
        '--add-opens=java.base/java.io=ALL-UNNAMED',
        '--add-opens=java.base/sun.misc=ALL-UNNAMED'
    ]
}

configurations {
    sharedDependency
    http4Dependency
    http5Dependency

    implementation {
        extendsFrom sharedDependency
    }
}

// Define source sets
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
            // Base source set excludes both HTTP client implementations
            exclude 'client/http4/**', 'client/http5/**'
        }
    }
    
    http4 {
        java {
            srcDirs = ['src/main/java']
            // Include only common files and HTTP4 files
            include 'client/http4/**'
            include '**/*.java'
            exclude 'client/http5/**'
        }
    }
    
    http5 {
        java {
            srcDirs = ['src/main/java']
            // Include only common files and HTTP5 files
            include 'client/http5/**'
            include '**/*.java'
            exclude 'client/http4/**'
        }
    }
}

dependencies {
    sharedDeps.each { d ->
        sharedDependency d
    }
    
    http4Deps.each { d ->
        http4Dependency d
    }
    
    http5Deps.each { d ->
        http5Dependency d
    }
}

jar {
    archiveBaseName.set("opensearchsql")
}

// Configure compile tasks for all source sets
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += project.ext.jvmArgs
}

// Configure HTTP4 and HTTP5 compile tasks
tasks.named('compileHttp4Java') {
    classpath = configurations.http4Dependency + configurations.sharedDependency + sourceSets.main.compileClasspath
    // Skip this task when using v3 or above
    onlyIf { 
            task -> 
                def result = !project.ext.isV3orAbove
                if (result) {
                    println "Using HTTP4 client for version ${project.ext.getEffectiveVersion()}"
                }
                return result
        }
}

tasks.named('compileHttp5Java') {
    classpath = configurations.http5Dependency + configurations.sharedDependency + sourceSets.main.compileClasspath
    // Skip this task when using below v3
    onlyIf { 
            task -> 
                def result = project.ext.isV3orAbove
                if (result) {
                    println "Using HTTP5 client for version ${project.ext.getEffectiveVersion()}"
                }
                return result
        }
}

// Configure the classes task to depend on the appropriate compile task
tasks.named('classes').configure {
    dependsOn {
        project.ext.isV3orAbove ? ['compileHttp5Java'] : ['compileHttp4Java']
    }
}

applicationDefaultJvmArgs = project.ext.jvmArgs

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**', 'src/main/gen/**'
        }
        importOrder()
        licenseHeader("/*\n" +
                " * Copyright OpenSearch Contributors\n" +
                " * SPDX-License-Identifier: Apache-2.0\n" +
                " */\n\n")
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        googleJavaFormat('1.17.0').reflowLongStrings().groupArtifact('com.google.googlejavaformat:google-java-format')
    }
}


def createShadowJarTask(String taskName, String versionLabel, Configuration config) {
    tasks.register(taskName, com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
        archiveBaseName.set("opensearchsqlcli-${versionLabel}")
        configurations = [config]
        
        // Include the main source set output
        from(sourceSets.main.output)
        
        // Include the appropriate HTTP client source set based on version
        def httpClientInfo = project.ext.getHttpClientInfo()
        from(httpClientInfo.sourceSet.output)
        
        // Configure shadow jar settings
        manifest {
            attributes 'Main-Class': 'Gateway'
        }
        
        // Exclude signature files to avoid conflicts
        project.ext.metaInfExclusions.each { pattern ->
            exclude pattern
        }
        
        mergeServiceFiles()
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        zip64 = true
    }
}

// Create a shadow jar task for local JAR files
def createLocalShadowJarTask(String taskName, String versionLabel, String localJarDir) {
    def localConfigName = "${versionLabel.replace('.', '_')}_local"
    def localDependencyConfigName = "${localConfigName}_Dependency"

    tasks.register(taskName, com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
        archiveBaseName.set("opensearchsqlcli-${versionLabel}")

        // Include all dependencies
        configurations = [
            configurations.sharedDependency,
            configurations[localConfigName],
            configurations[localDependencyConfigName]
        ]
        
        doFirst {
            println "Including dependencies from sharedDependency:"
            configurations.sharedDependency.each { file ->
                println "  - ${file.name}"
            }
        }
        
        // Include the main source set output
        from(sourceSets.main.output)
        
        // Include the appropriate HTTP client source set based on version
        def httpClientInfo = project.ext.getHttpClientInfo()
        from(httpClientInfo.sourceSet.output)
        
        // Include HTTP client dependencies
        configurations += [httpClientInfo.dependencies]
        
        // Configure shadow jar settings
        manifest {
            attributes 'Main-Class': 'Gateway'
        }
        
        // Exclude signature files to avoid conflicts
        project.ext.metaInfExclusions.each { pattern ->
            exclude pattern
        }
        
        mergeServiceFiles()
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        zip64 = true
    }
}

// Helper function to configure runtime configuration based on version
ext.configureRuntimeConfiguration = { String version, String configName, String dependencyConfigName ->
    def httpClientInfo = project.ext.getHttpClientInfo()
    
    configurations[dependencyConfigName].extendsFrom(
        configurations.sharedDependency, 
        httpClientInfo.dependencies,
        configurations[configName]
    )
    
    println "Version ${version}: Using ${httpClientInfo.clientName} dependencies and source files"
}

ext.createVersionConfigurations = { String version ->
    def configName = "${version.replace('.', '_')}"
    def dependencyConfigName = "${configName}_Dependency"

    if (!configurations.findByName(configName)) {
        configurations.create(configName)
        configurations.create(dependencyConfigName)
        
        // Update the global isV3orAbove variable
        project.ext.updateIsV3orAbove(version)
        
        // Configure runtime configuration based on version
        configureRuntimeConfiguration(version, configName, dependencyConfigName)

        // Add unified-query dependencies
        project.ext.mavenSubmodules.each { name ->
            dependencies.add(configName, "org.opensearch.query:unified-query-${name}:${version}-SNAPSHOT")
        }
        
        // Add datasources JAR file
        dependencies.add(configName, files('submodule/datasources-3.1.0.0-SNAPSHOT.jar'))

        // Add OpenSearch client dependencies with appropriate version
        def clientVersion = project.ext.getOpenSearchClientVersion(version)
        
        project.ext.opensearchClientDeps.each { dep ->
            dependencies.add(configName, "org.opensearch.client:${dep}:${clientVersion}")
        }
        
        println "Version ${version}: Using OpenSearch client version ${clientVersion}"

        // Create shadow jar task for this version
        createShadowJarTask(configName, version, configurations[dependencyConfigName])

        println "Version ${version}: Created configuration and task name as ${configName}"
    }

    return configName
}

// Create configurations for local JAR files
ext.createLocalConfigurations = { String version, String localJarDir ->
    def configName = "${version.replace('.', '_')}_local"
    def dependencyConfigName = "${configName}_Dependency"

    if (!configurations.findByName(configName)) {
        configurations.create(configName)
        configurations.create(dependencyConfigName)
        
        // Update the global isV3orAbove variable
        project.ext.updateIsV3orAbove(version)
        
        // Configure runtime configuration based on version
        configureRuntimeConfiguration(version, configName, dependencyConfigName)
        
        // Add each submodule JAR file to the configuration
        project.ext.submodules.each { submodule ->
            def submoduleJarPath = "${localJarDir}/${submodule}/build/libs/${submodule}-${version}-SNAPSHOT.jar"
            def submoduleJarFile = new File(submoduleJarPath)
            if (submoduleJarFile.exists()) {
                println "Adding submodule JAR file to configuration: ${submoduleJarPath}"
                dependencies.add(configName, files(submoduleJarFile))
            } else {
                println "Warning: Submodule JAR file not found: ${submoduleJarPath}"
            }
        }
        
        // Add OpenSearch client dependencies with appropriate version
        def clientVersion = project.ext.getOpenSearchClientVersion(version)
        
        project.ext.opensearchClientDeps.each { dep ->
            dependencies.add(configName, "org.opensearch.client:${dep}:${clientVersion}")
        }
        
        // Add OpenSearch core dependency
        dependencies.add(configName, "org.opensearch:opensearch:${clientVersion}")
        
        println "Version ${version}: Using OpenSearch client version ${clientVersion}"
        
        // Create shadow jar task for this version
        createShadowJarTask(configName, version, configurations[dependencyConfigName])
        
        println "Version ${version}: Created configuration and task name as ${configName}"
    }
    
    return configName
}

// Use Gradle's task rules for dynamic task creation
tasks.addRule("Pattern: <version>: Creates a task for the specified version") { String taskName ->
    if (taskName.count("_") >= 1 && taskName.matches("[0-9].*")) {
        def version = taskName.replace('_', '.')
        
        // Check if it's a local JAR task
        if (project.ext.isLocalJarTask(taskName)) {
            // Extract the version without the _local suffix
            version = taskName.substring(0, taskName.lastIndexOf("_local")).replace('_', '.')
            
            // Get the local JAR directory
            def localJarDir = project.ext.getLocalJarDir()
            
            if (localJarDir == null) {
                throw new GradleException("Local JAR directory not specified. Use -PlocalJarDir=/path/to/local/jar")
            }
            
            // Create configurations for local JAR files
            def localConfigName = project.ext.createLocalConfigurations(version, localJarDir)
            
            // Configure implementation directly
            def httpClientInfo = project.ext.getHttpClientInfo()
            configurations.implementation.extendsFrom = [
                configurations.sharedDependency, 
                httpClientInfo.dependencies, 
                configurations[localConfigName]
            ] as Set
            
            println "Version ${version}: Set implementation configuration for local JAR using directory ${localJarDir}"
        } else {
            def configName = project.ext.createVersionConfigurations(version)
            
            // Configure implementation directly
            def httpClientInfo = project.ext.getHttpClientInfo()
            configurations.implementation.extendsFrom = [
                configurations.sharedDependency, 
                httpClientInfo.dependencies, 
                configurations[configName]
            ] as Set
            
            println "Version ${version}: Set implementation configuration"
        }
    }
}

shadowJar {
    zip64 = true
}

// Set up default configuration when no specific version task is used
// e.g. ./gradlew build so that IDE can recognize import classes
afterEvaluate {
    def effectiveVersion = project.ext.getEffectiveVersion()
    
    if (effectiveVersion == project.ext.defaultVersion) {

        def configName = project.ext.createVersionConfigurations(effectiveVersion)
        def httpClientInfo = project.ext.getHttpClientInfo()
        configurations.implementation.extendsFrom = [
            configurations.sharedDependency, 
            httpClientInfo.dependencies, 
            configurations[configName]
        ] as Set
        println "Default build: Set up version ${effectiveVersion} with ${httpClientInfo.clientName} client"
    }
}
